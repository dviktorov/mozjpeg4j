apply plugin: "java"
apply plugin: "application"
//apply plugin: 'groovy'
//apply plugin: "project-report"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = "mozjpeg"
def version = "1.1"

ext {
    slf4jVersion = "1.7.6"
    junitVersion = "4.12"
}
 
repositories {
	mavenCentral()
}
 
dependencies {

    compile "org.slf4j:slf4j-api:" + slf4jVersion
    compile "org.slf4j:slf4j-log4j12:" + slf4jVersion

    testCompile "junit:junit:" + junitVersion

}

test {
    systemProperty "mozjpeg.native.lib.internal.renewed", "true"
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
//    afterTest { desc, result ->
//        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
//    }
}

task runJpegCompressionDemoApp (dependsOn: 'classes', type: JavaExec) {
    main = "org.libjpegturbo.turbojpeg.app.MozJpegGalleryApp"
    args project.testReportDir
    classpath = sourceSets.test.runtimeClasspath
    description = "Extended compression test"
}

task runMozJpeg (dependsOn: 'classes', type: JavaExec) {
    main = "org.libjpegturbo.turbojpeg.app.MozJpegApp"
    classpath = sourceSets.test.runtimeClasspath
    description = "Example of a consol application"
}

jar {
    manifest {
        attributes(
            "Manifest-Version": "1.0",
            "Specification-Title": "MozJpeg library for Java (version 3.1)",
            "Specification-Version": version,
            "Created-By": "Dmitry G. Viktorov",
            "Main-Class": "org.libjpegturbo.turbojpeg.utils.MozJpegApp",
            "Class-Path": configurations.compile.collect { it.getName() }.join(" ")
        )
    }
    baseName = project.name + "-" + version
}


task jarOSGI (dependsOn: "classes", type: Jar) {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Bnd-LastModified": new Date().time,
                "Bundle-ClassPath": ".",
                "Bundle-Description": "MozJpeg4J OSGI bundle (MozJpeg version 3.1)",
                "Bundle-ManifestVersion": "2",
                "Bundle-Name": "MozJpeg4J (MozJpeg version 3.1)",
                "Bundle-SymbolicName": "org.libjpegturbo.turbojpeg",
                "Bundle-Version": version,
                "Created-By": "Dmitry G. Viktorov",
                "Originally-Created-By": "Dmitry G. Viktorov",
                "Main-Class": "org.libjpegturbo.turbojpeg.utils.MozJpegApp",
                "Export-Package": 'org.libjpegturbo.turbojpeg;version="' + version + '",org.libjpegturbo.turbojpeg.processor.api;version="' + version + '",org.libjpegturbo.turbojpeg.processor.impl;version="' + version + '"',
                "Import-Package": "org.slf4j;resolution:=optional",
                "Tool": "Bnd-1.50.0"
        )
    }
    classifier = "osgi"
    baseName = project.name + "-" + version
    from sourceSets.main.output
}



